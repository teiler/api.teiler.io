plugins {
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.8"
    id "com.github.ben-manes.versions" version "0.15.0"
    id "org.sonarqube" version "2.2"
}

apply plugin: "application"
apply plugin: "java"
apply plugin: "distribution"

apply plugin: "checkstyle"
apply plugin: "jacoco"

apply plugin: "eclipse"
apply plugin: "idea"

sourceCompatibility = 1.8
sourceSets.main.java.srcDirs = ["src/main/java", "src/main/resources", "src/dist/sql"]
sourceSets.test.java.srcDirs = ["src/test/java", "src/test/resources"]

mainClassName = "io.teiler.server.Tylr"

configurations {
    codacy
}

repositories {
    mavenLocal()
    jcenter()
}

ext {
    sparkVersion = "2.6.0"
    springBootVersion = "1.5.7.RELEASE"

    postgresqlVersion = "42.1.4"
    hibernateValidatorVersion = "6.0.2.Final"
    queryDslVersion = "4.1.4"

    gsonVersion = "2.8.2"

    slf4jVersion = "1.7.24"
    logbackVersion = "1.2.3"

    junitVersion = "4.12"
    restAssuredVersion = "3.0.3"
    h2Version = "1.4.196"

    codacyCoverageReporterVersion = "2.0.1"
}

dependencies {
    compile "com.sparkjava:spark-core:${sparkVersion}"
    compile "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"

    compile "org.postgresql:postgresql:${postgresqlVersion}"
    compile "org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}"
    compile "com.querydsl:querydsl-jpa:${queryDslVersion}"
    compile "com.querydsl:querydsl-apt:${queryDslVersion}"

    compile "com.google.code.gson:gson:${gsonVersion}"

    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"

    testCompile "junit:junit:${junitVersion}"
    testCompile "io.rest-assured:rest-assured:${restAssuredVersion}"
    testCompile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testCompile "com.h2database:h2:${h2Version}"

    codacy "com.codacy:codacy-coverage-reporter:${codacyCoverageReporterVersion}"
}

distributions {
    main {
        contents {
            from { "application.properties-example" }
        }
    }
}

checkstyle {
    configFile = rootProject.file("checkstyle.xml")
    toolVersion = "7.6"
    sourceSets = [sourceSets.main, sourceSets.test]
}

querydsl {
    jpa true
}

test {
    exclude "**/*EndpointController*"

    reports {
        junitXml.enabled true
        html.enabled false
    }
}

jacocoTestReport {
    executionData(
        file("${buildDir}/jacoco/test.exec"),
        file("${buildDir}/jacoco/integrationTest.exec")
    )

    reports {
        html.enabled false
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacocoTestReport.xml")
    }
}

tasks.withType(Checkstyle) {
    exclude "src/querydsl/java/**"

    reports {
        xml.enabled false
        html.enabled false
    }
}

task integrationTest(type: Test) {
    include "**/EndpointControllerTestSuite.class"

    reports {
        junitXml.enabled true
        html.enabled false
    }
}

task pushCoverageReportToCodacy(type: JavaExec) {
    dependsOn test
    dependsOn integrationTest
    dependsOn jacocoTestReport

    onlyIf {
        System.env["CODACY_PROJECT_TOKEN"] != null
    }
    
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
        "-l",
        "Java",
        "-r",
        "${buildDir}/reports/jacocoTestReport.xml"
    ]
}